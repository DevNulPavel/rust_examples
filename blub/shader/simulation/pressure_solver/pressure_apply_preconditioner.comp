// Applies preconditioner to the Residual, yielding the auxiliary vector usually denoted as z,
// and starts dotproduct (component-wise multiply) of z with Residual (r)

#version 460

#include "pressure.glsl"

layout(set = 2, binding = 0) buffer restrict _ReduceBuffer { float ReduceBuffer[]; };
layout(set = 2, binding = 1) uniform texture3D Residual;

layout(set = 2, binding = 2, r32f) uniform restrict writeonly image3D AuxiliaryOrTemp;
layout(set = 2, binding = 3) uniform texture3D ResidualOrTemp;

COMPUTE_PASS_PRESSURE

// It seems that there is a bug that will make this function misbehave if it is declared before the thread group layout declaration
uint GetReduceBufferAddress() {
    uint threadsPerWorkgroup = gl_WorkGroupSize.x * gl_WorkGroupSize.y * gl_WorkGroupSize.z;
    uint threadGroupIndex = (gl_WorkGroupID.z * gl_NumWorkGroups.y + gl_WorkGroupID.y) * gl_NumWorkGroups.x + gl_WorkGroupID.x;
    return gl_LocalInvocationIndex + threadsPerWorkgroup * threadGroupIndex;
}

#define PRECONDITIONER_PASS0 0
#define PRECONDITIONER_PASS1 1

// Incomplete Poisson preconditioner
// see https://software.intel.com/content/www/us/en/develop/articles/parallelized-incomplete-poisson-preconditioner-in-cloth-simulation.html
// and also https://github.com/tunabrain/gpu-fluid
// as well as https://github.com/austinEng/WebGL-PIC-FLIP-Fluid#pressure-solve

// Steps:
// * ztemp[i] = r[i] - r * L[i, :] * 1/d
// * z[i] = ztemp[i] - ztemp * L[i, :] * 1/d
// -> Same shader with slightly different inputs

void main() {
    ivec3 gridCoord = ivec3(gl_GlobalInvocationID);
    uint globalLinearTid = GetReduceBufferAddress();

    float marker = texelFetch(MarkerVolume, gridCoord, 0).x;
    if (marker != CELL_FLUID) {
        if (PushConstants.Mode != PRECONDITIONER_PASS0) {
            ReduceBuffer[globalLinearTid] = 0.0; // Note that ReduceBuffer is used in ping-pong style reduction, need to rezero every time!
        }
        return;
    }

    float result = texelFetch(ResidualOrTemp, gridCoord, 0).x;

    float markerX0 = texelFetch(MarkerVolume, gridCoord - ivec3(1, 0, 0), 0).x;
    float markerX1 = texelFetch(MarkerVolume, gridCoord + ivec3(1, 0, 0), 0).x;
    float markerY0 = texelFetch(MarkerVolume, gridCoord - ivec3(0, 1, 0), 0).x;
    float markerY1 = texelFetch(MarkerVolume, gridCoord + ivec3(0, 1, 0), 0).x;
    float markerZ0 = texelFetch(MarkerVolume, gridCoord - ivec3(0, 0, 1), 0).x;
    float markerZ1 = texelFetch(MarkerVolume, gridCoord + ivec3(0, 0, 1), 0).x;

    if (markerX0 == CELL_FLUID) {
        result -= texelFetch(ResidualOrTemp, gridCoord - ivec3(1, 0, 0), 1).x;
    }
    if (markerY0 == CELL_FLUID) {
        result -= texelFetch(ResidualOrTemp, gridCoord - ivec3(0, 1, 0), 1).x;
    }
    if (markerZ0 == CELL_FLUID) {
        result -= texelFetch(ResidualOrTemp, gridCoord - ivec3(0, 0, 1), 1).x;
    }

    float numNonSolidNeighbors = 0.0;
    numNonSolidNeighbors += float(markerX0 != CELL_SOLID);
    numNonSolidNeighbors += float(markerX1 != CELL_SOLID);
    numNonSolidNeighbors += float(markerY0 != CELL_SOLID);
    numNonSolidNeighbors += float(markerY1 != CELL_SOLID);
    numNonSolidNeighbors += float(markerZ0 != CELL_SOLID);
    numNonSolidNeighbors += float(markerZ1 != CELL_SOLID);
    if (numNonSolidNeighbors > 0.0)
        result /= numNonSolidNeighbors;

    imageStore(AuxiliaryOrTemp, gridCoord, result.xxxx);

    if (PushConstants.Mode != PRECONDITIONER_PASS0) {
        ReduceBuffer[globalLinearTid] = result * texelFetch(Residual, gridCoord, 0).x;
    }
}
