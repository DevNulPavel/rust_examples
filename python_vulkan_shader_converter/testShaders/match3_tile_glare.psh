varying PRECISION_HIGH vec2  v_texcoord;
uniform sampler2D       u_texture;
uniform PRECISION_HIGH vec4  u_color;
uniform PRECISION_HIGH float u_brightness;

uniform PRECISION_HIGH float u_height_correction_factor;
uniform PRECISION_HIGH float u_argument_addition;
uniform PRECISION_HIGH float u_angle_k;
varying PRECISION_HIGH vec2  v_fragment_screen_coords;

void main(void)
{
    PRECISION_LOW vec4 color = texture2D(u_texture, v_texcoord) * u_color * u_brightness;
    PRECISION_HIGH float y = u_angle_k * (v_fragment_screen_coords.x - u_argument_addition);
    PRECISION_HIGH float dy = abs(y - v_fragment_screen_coords.y);

    PRECISION_HIGH float glow_length = 0.2 * u_height_correction_factor;
    PRECISION_HIGH float line_length = 0.02 * u_height_correction_factor;
    PRECISION_HIGH float line_highlight_strength = 0.68;
    PRECISION_HIGH float glow_strength = 0.4;
    PRECISION_HIGH float glow_modifier = clamp((exp( (-dy / glow_length) / 0.5)), 0.0, 1.0);

    color.rgb += color.rgb * (step(dy, line_length) * line_highlight_strength + glow_modifier * glow_strength) * color.a;

    gl_FragColor = color;
}

